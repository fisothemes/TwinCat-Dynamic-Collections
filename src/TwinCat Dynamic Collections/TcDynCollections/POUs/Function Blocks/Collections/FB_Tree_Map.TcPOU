<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Tree_Map" Id="{e2ff5b26-f7a8-0439-0ee5-4ef58aef733c}" SpecialFunc="None">
    <Declaration><![CDATA[// A collection of key-value pair items implemented using an iterative AVL Tree. 
// Keys and values can be retrieved as an immutable list (whose base is FB_ArrayList) via 4 traversal methods; Inorder, Preorder, Postorder and Level Order.
// This data structure was implemented with the help of Max Goren's iterative AVL tree in C++. See Function Block's body for more details.
{attribute 'enable_dynamic_creation'}
{attribute 'no_assign'}
{attribute 'no_explicit_call' := 'direct call for this function block is not allowed'}
FUNCTION_BLOCK FB_Tree_Map EXTENDS FB_Collection IMPLEMENTS I_Tree_Map //, I_Enumerable, I_Copyable
VAR
	pRoot : POINTER TO T_AVL_Map_Node;
	Traversal : T_BST_Traversal := T_BST_Traversal.In_Order; 
	pKeys, pValues : POINTER TO FB_Array_List;
	fbImmutable_Keys_List,
	fbImmutable_Values_List : FB_Immutable_List;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*
	https://github.com/maxgoren
	https://github.com/maxgoren/Algorithms/
	http://www.maxgcoding.com/iterative-avl-trees-insertion/
	http://www.maxgcoding.com/iterative-avl-trees-deletion/

	MIT License
	
	Copyright (c) 2021 Max Goren
	
	Permission is hereby granted, free of charge, to any person obtaining a copy
	of this software and associated documentation files (the "Software"), to deal
	in the Software without restriction, including without limitation the rights
	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
	copies of the Software, and to permit persons to whom the Software is
	furnished to do so, subject to the following conditions:
	
	The above copyright notice and this permission notice shall be included in all
	copies or substantial portions of the Software.
	
	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
	SOFTWARE.
*)]]></ST>
    </Implementation>
    <Folder Name="Internal" Id="{842a1341-2547-0aaf-2013-3735073998a0}" />
    <Property Name="_Traversal" Id="{13bbbd2a-6700-08a3-3ce9-c5658db2c73c}">
      <Declaration><![CDATA[// Traversal method for retrieving keys and values in the tree map set.
PROPERTY _Traversal : T_BST_Traversal]]></Declaration>
      <Get Name="Get" Id="{9697d400-7278-0f1c-16e2-4a14e707564c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_Traversal := THIS^.Traversal;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{499cf2b9-3fbe-0a97-0d87-add8fc458e0d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[THIS^.Traversal := _Traversal;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="Balance" Id="{5fe76669-cb2a-044f-2f4a-37aaec61b932}" FolderPath="Internal\">
      <Declaration><![CDATA[// Performs rotation and updates heights until tree is balanced.
METHOD PROTECTED Balance
VAR_INPUT
    pNode : POINTER TO T_AVL_Map_Node;
END_VAR
VAR 
	pTmp : POINTER TO T_AVL_Map_Node;
    Balance_Factor : T_Position;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[WHILE pNode <> 0 AND_THEN pNode^.pParent <> 0 DO
	pTmp := pNode^.pParent;
	
	THIS^.Update_Height(pTmp);
	
	IF THIS^.Get_Balance(pTmp) < -1 THEN
		IF THIS^.Get_Balance(pTmp^.pRight) > 0 THEN THIS^.Rotate_Right(pTmp^.pRight); END_IF
		THIS^.Rotate_Left(pTmp);
		
	ELSIF THIS^.Get_Balance(pTmp) > 1 THEN
		IF THIS^.Get_Balance(pTmp^.pLeft) < 0 THEN THIS^.Rotate_Left(pTmp^.pLeft); END_IF
		THIS^.Rotate_Right(pTmp);
		END_IF
	
	pNode := pNode^.pParent;
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Clear" Id="{10137b2f-c0aa-0b09-3b55-b9c201f98072}">
      <Declaration><![CDATA[// Empties/Clears/Deletes every item in the map.
METHOD Clear : I_Map
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Clear := THIS^;

THIS^.Clear_Node(THIS^.pRoot);

IF THIS^.pKeys <> 0 THEN 
	THIS^.fbImmutable_Keys_List(ipList := 0); 
	__DELETE(THIS^.pKeys); END_IF

IF THIS^.pValues <> 0 THEN
	THIS^.fbImmutable_Values_List(ipList := 0);
	__DELETE(THIS^.pValues);
	END_IF

THIS^.pRoot := 0;
THIS^._Count := 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Clear_Node" Id="{ac078b46-0ea9-025c-3ca8-c6230e380295}" FolderPath="Internal\">
      <Declaration><![CDATA[// Deletes the node and its tree/sub-trees.
{warning disable C0394}
METHOD PROTECTED Clear_Node
VAR_INPUT
	pRoot : POINTER TO T_AVL_Map_Node;
END_VAR
VAR
	fbNode_Stack : FB_Stack;
	pTmp : POINTER TO T_AVL_Map_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pRoot = 0 THEN RETURN; END_IF

fbNode_Stack.Push(pRoot);

WHILE NOT fbNode_Stack._Empty DO
	fbNode_Stack.Get(pTmp).Pop();
	
	IF pTmp^.pRight <> 0 THEN fbNode_Stack.Push(pTmp^.pRight); END_IF
	IF pTmp^.pLeft <> 0 THEN fbNode_Stack.Push(pTmp^.pLeft); END_IF
	
	THIS^.Delete_Node(pTmp);
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Contains" Id="{3116e2f2-f498-05d7-0cd6-f105b88cf8fe}">
      <Declaration><![CDATA[// Checks if key is contained in the map.
METHOD Contains : BOOL
VAR_INPUT
	Key : ANY; // Key to find.
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Contains := THIS^.Search_Node(THIS^.pRoot, Key) <> 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Create_Node" Id="{485948aa-ed06-09e9-287c-4b61717f5a06}" FolderPath="Internal\">
      <Declaration><![CDATA[// Creates a new node in dynamic memory with a clone of the generic item as it data.
METHOD PROTECTED Create_Node : POINTER TO ST_AVL_MAP_NODE
VAR_INPUT
	Key, Value : T_Generic;
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
	Error : T_Error;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Create_Node := __NEW(ST_AVL_MAP_NODE);

IF Create_Node = 0 THEN Error := GVL_Errors.ERR_MALLOC_FAILED; RETURN; END_IF

Create_Node^.Key := F_Clone_Generic(Key, bSuccess => bSuccess, Error => Error);

IF NOT bSuccess THEN 
	__DELETE(Create_Node);
	RETURN;
	END_IF

Create_Node^.Value := F_Clone_Generic(Value, bSuccess => bSuccess, Error => Error);

IF NOT bSuccess THEN 
	F_Delete_Generic(Create_Node^.Key);
	__DELETE(Create_Node);
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Delete_Node" Id="{417d91b1-8ed2-0c8d-2317-1f101853a501}" FolderPath="Internal\">
      <Declaration><![CDATA[// Safety deletes node.
// If the node and/or it's value/data is in dynamic memory it/they will be deleted.
METHOD PROTECTED Delete_Node
VAR_INPUT
	pNode : REFERENCE TO POINTER TO T_AVL_Map_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (pNode = 0) THEN RETURN; END_IF 

F_Delete_Generic(pNode^.Key);
F_Delete_Generic(pNode^.Value);

CASE Tc2_System.F_CheckMemoryArea(pNode, 1) OF
	Tc2_System.E_TcMemoryArea.Dynamic: __DELETE(pNode);
ELSE
	pNode := 0;
	END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_exit" Id="{e0a490c8-5201-0362-3f6c-8cce603b5f24}">
      <Declaration><![CDATA[{attribute 'hide'}
METHOD FB_exit : BOOL
VAR_INPUT
	bInCopyCode : BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// If this is an online change or this collection is empty then ignore. 
IF bInCopyCode THEN RETURN; END_IF

THIS^.Clear();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Find_Minimum_Node" Id="{bd4c204f-03ac-0fd5-3f2c-a6031d179035}" FolderPath="Internal\">
      <Declaration><![CDATA[// Finds the node with the smallest item in a tree or sub-tree.
METHOD PROTECTED Find_Minimum_Node : POINTER TO T_AVL_Node
VAR_INPUT
	pNode : POINTER TO T_AVL_Map_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pNode = 0 THEN RETURN; END_IF

WHILE pNode^.pLeft <> 0 DO
	pNode := pNode^.pLeft;
	END_WHILE
	
Find_Minimum_Node := pNode;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get" Id="{c45ebf13-7d1b-065d-1bb6-e01bcff0e92c}">
      <Declaration><![CDATA[// Gets the value associated with the specified key without removing it from the map.
METHOD Get : I_Map
VAR_INPUT
	Key : ANY; // Key used to identify stored value
	Value : ANY; // Variable to store requested data 
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
END_VAR
VAR
	Tmp : T_Generic;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Get := THIS^;

THIS^.Get_Generic(Key, Value => Tmp, bSuccess => bSuccess);

IF NOT bSuccess THEN RETURN; END_IF

IF NOT F_Matching_Generics(Value, Tmp) THEN
	THIS^._Error_Status := GVL_Errors.ERR_TYPE_MISMATCH;
	RETURN;
	END_IF
	
F_Copy_Generic_Value(Tmp, Value, bSuccess => bSuccess, Error => Error);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_As_String" Id="{329e2689-e23c-0891-08ec-172f73c0584f}">
      <Declaration><![CDATA[// Returns value as a string, if type to string conversion is supported.
METHOD Get_As_String : I_Map
VAR_INPUT
	Key : ANY; // Key used to identify stored value
	sValue : REFERENCE TO STRING; // Variable to store returned item.
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
END_VAR
VAR
	Tmp : T_Generic;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Get_As_String := THIS^;

IF NOT __ISVALIDREF(sValue) THEN 
	THIS^._Error_Status := GVL_Errors.ERR_INVALID_REFERENCE;
	RETURN;
	END_IF
	
THIS^.Get_Generic(Key, Value => Tmp, bSuccess => bSuccess);

IF NOT bSuccess THEN RETURN; END_IF
	
CASE Tmp.TypeClass OF
	T_Type.TYPE_STRING:
		Tc2_Utilities.STRNCPY(ADR(sValue), Tmp.pValue, MIN(Tc2_Utilities.LEN2(ADR(sValue))+1, TO_UDINT(Tmp.diSize)));
		THIS^.Clear_Error();

	T_Type.TYPE_WSTRING:
		Tc2_Utilities.WSTRING_TO_STRING2(ADR(sValue), Tmp.pValue, MIN(Tc2_Utilities.LEN2(ADR(sValue))+1, TO_UDINT(Tmp.diSize)));
		THIS^.Clear_Error();
ELSE
	sValue := F_Generic_To_String(Tmp, bSuccess => bSuccess, Error => THIS^.Error);
	END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_Balance" Id="{55dbc1de-460f-0e73-2e25-c4e34d60bb26}" FolderPath="Internal\">
      <Declaration><![CDATA[// Gets the difference between the left and right sub-trees.
METHOD PROTECTED Get_Balance : T_Position
VAR_INPUT
	pNode : POINTER TO T_AVL_Map_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Get_Balance :=  THIS^.Get_Height(pNode^.pLeft) - THIS^.Get_Height(pNode^.pRight);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_Generic" Id="{404f2036-5e48-0862-0e2a-030cae0b2956}">
      <Declaration><![CDATA[// Gets the value in its generic form associated with the specified key in its generic form without removing it from the map.
// (** Use carefully as this contains the pointer of the actual value in the collection. **)
// If you wish to use this it is recommeded you create a copy of the returned value using MEMMOVE or MEMCPY
METHOD Get_Generic : I_Map
VAR_INPUT
	Key : T_Generic; // Key used to identify stored value
END_VAR
VAR_OUTPUT
	Value : T_Generic := (TypeClass := T_Type.TYPE_NONE); // Variable to store requested data 
	bSuccess : BOOL;
END_VAR
VAR
	pTmp : POINTER TO T_AVL_Map_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Get_Generic := THIS^;

pTmp := THIS^.Search_Node(THIS^.pRoot, Key);
bSuccess := pTmp <> 0;

IF NOT bSuccess THEN 
	// Throw item not found error.
	THIS^._Error_Status := GVL_Errors.ERR_NO_KEY;
	RETURN; 
	END_IF

Value := pTmp^.Value;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_Height" Id="{4116f697-9b5d-0613-3c8f-4e041dbbad46}" FolderPath="Internal\">
      <Declaration><![CDATA[// Safely gets the height of a sub-tree.
METHOD PROTECTED Get_Height : T_Position
VAR_INPUT
	pNode : POINTER TO T_AVL_Map_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Get_Height := SEL(pNode <> 0, -1, pNode^.Height);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_Keys" Id="{d30c7356-1cec-0a4f-16b8-86ed85f0a9eb}">
      <Declaration><![CDATA[// Gets an immutable list of the keys.
{warning disable C0394}
METHOD Get_Keys : I_Immutable_List
VAR
	pCurrent : POINTER TO T_AVL_Map_Node := THIS^.pRoot;
	pPrevious, pTmp : POINTER TO T_AVL_Map_Node;
	fbNode_Buffer : FB_Queue;
	bSuccess : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF THIS^.pKeys = 0 THEN THIS^.pKeys := __NEW(FB_Array_List(Initial_Capacity := 0)); END_IF
THIS^.fbImmutable_Keys_List(ipList := THIS^.pKeys^);
Get_Keys := THIS^.fbImmutable_Keys_List;

THIS^.pKeys^.Clear();

IF pCurrent = 0 THEN RETURN; END_IF

WHILE (NOT fbNode_Buffer._Empty) OR_ELSE pCurrent <> 0 DO
	
	CASE THIS^._Traversal OF
		T_BST_Traversal.In_Order:
			IF pCurrent <> 0 THEN 
				fbNode_Buffer.Enqueue_At_Front(pCurrent);
				pCurrent := pCurrent^.pLeft;
			ELSE
				fbNode_Buffer.Get(pCurrent).Dequeue();
				THIS^.pKeys^.Insert_Generic(THIS^.pKeys^._Count, pCurrent^.Key);
				pCurrent := pCurrent^.pRight;
				END_IF
				
		T_BST_Traversal.Pre_Order:
			IF pCurrent <> 0 THEN 
				THIS^.pKeys^.Insert_Generic(THIS^.pKeys^._Count, pCurrent^.Key);
				IF pCurrent^.pRight <> 0 THEN fbNode_Buffer.Enqueue_At_Front(pCurrent^.pRight); END_IF
				pCurrent := pCurrent^.pLeft;
			ELSE
				fbNode_Buffer.Get(pCurrent).Dequeue();
				END_IF
			
		T_BST_Traversal.Post_Order:
			WHILE pCurrent <> 0 DO 
				fbNode_Buffer.Enqueue_At_Front(pCurrent);
				pCurrent := pCurrent^.pLeft;
				END_WHILE
				
			IF NOT fbNode_Buffer._Empty THEN
				fbNode_Buffer.Get(pTmp);
				pTmp := pTmp^.pRight;
				IF pTmp = 0 OR_ELSE pTmp = pPrevious THEN 
					fbNode_Buffer.Get(pCurrent).Dequeue();
					THIS^.pKeys^.Insert_Generic(THIS^.pKeys^._Count, pCurrent^.Key);
					pPrevious := pCurrent;
					pCurrent := 0;
				ELSE
					pCurrent := pTmp;
					END_IF
				END_IF
			
		T_BST_Traversal.Level_Order:
			THIS^.pKeys^.Insert_Generic(THIS^.pKeys^._Count, pCurrent^.Key);
			IF pCurrent^.pLeft <> 0 THEN fbNode_Buffer.Enqueue(pCurrent^.pLeft); END_IF
			IF pCurrent^.pRight <> 0 THEN fbNode_Buffer.Enqueue(pCurrent^.pRight); END_IF
			fbNode_Buffer.Get(pCurrent).Dequeue(bSuccess => bSuccess);
			IF NOT bSuccess THEN pCurrent := 0; END_IF
	ELSE
		RETURN;
		END_CASE
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Get_Values" Id="{4ccfb948-869f-0373-02b4-5b4c2426f5ba}">
      <Declaration><![CDATA[// Gets an immutable list of the values.
{warning disable C0394}
METHOD Get_Values : I_Immutable_List
VAR
	pCurrent : POINTER TO T_AVL_Map_Node := THIS^.pRoot;
	pPrevious, pTmp : POINTER TO T_AVL_Map_Node;
	fbNode_Buffer : FB_Queue;
	bSuccess : BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF THIS^.pValues = 0 THEN THIS^.pValues := __NEW(FB_Array_List(Initial_Capacity := 0)); END_IF
THIS^.fbImmutable_Values_List(ipList := THIS^.pValues^);
Get_Values := THIS^.fbImmutable_Values_List;

THIS^.pValues^.Clear();

IF pCurrent = 0 THEN RETURN; END_IF

WHILE (NOT fbNode_Buffer._Empty) OR_ELSE pCurrent <> 0 DO
	
	CASE THIS^._Traversal OF
		T_BST_Traversal.In_Order:
			IF pCurrent <> 0 THEN 
				fbNode_Buffer.Enqueue_At_Front(pCurrent);
				pCurrent := pCurrent^.pLeft;
			ELSE
				fbNode_Buffer.Get(pCurrent).Dequeue();
				THIS^.pValues^.Insert_Generic(THIS^.pValues^._Count, pCurrent^.Value);
				pCurrent := pCurrent^.pRight;
				END_IF
				
		T_BST_Traversal.Pre_Order:
			IF pCurrent <> 0 THEN 
				THIS^.pValues^.Insert_Generic(THIS^.pValues^._Count, pCurrent^.Value);
				IF pCurrent^.pRight <> 0 THEN fbNode_Buffer.Enqueue_At_Front(pCurrent^.pRight); END_IF
				pCurrent := pCurrent^.pLeft;
			ELSE
				fbNode_Buffer.Get(pCurrent).Dequeue();
				END_IF
			
		T_BST_Traversal.Post_Order:
			WHILE pCurrent <> 0 DO 
				fbNode_Buffer.Enqueue_At_Front(pCurrent);
				pCurrent := pCurrent^.pLeft;
				END_WHILE
				
			IF NOT fbNode_Buffer._Empty THEN
				fbNode_Buffer.Get(pTmp);
				pTmp := pTmp^.pRight;
				IF pTmp = 0 OR_ELSE pTmp = pPrevious THEN 
					fbNode_Buffer.Get(pCurrent).Dequeue();
					THIS^.pValues^.Insert_Generic(THIS^.pValues^._Count, pCurrent^.Value);
					pPrevious := pCurrent;
					pCurrent := 0;
				ELSE
					pCurrent := pTmp;
					END_IF
				END_IF
			
		T_BST_Traversal.Level_Order:
			THIS^.pValues^.Insert_Generic(THIS^.pValues^._Count, pCurrent^.Value);
			IF pCurrent^.pLeft <> 0 THEN fbNode_Buffer.Enqueue(pCurrent^.pLeft); END_IF
			IF pCurrent^.pRight <> 0 THEN fbNode_Buffer.Enqueue(pCurrent^.pRight); END_IF
			fbNode_Buffer.Get(pCurrent).Dequeue(bSuccess => bSuccess);
			IF NOT bSuccess THEN pCurrent := 0; END_IF
	ELSE
		RETURN;
		END_CASE
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Insert" Id="{eb1130de-baf7-069b-2b6c-11044eef3cca}">
      <Declaration><![CDATA[// Insert an element with the provided key and value to the map.
METHOD Insert : I_Map
VAR_INPUT
	Key : ANY; // Key used to identify stored value
	Value : ANY; // Value to store in map with key
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Insert := THIS^.Insert_Generic(Key, Value, FALSE, bSuccess => bSuccess);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Insert_Generic" Id="{7c48d1a9-3f55-0f52-0658-9eba47124674}">
      <Declaration><![CDATA[// Insert an element with the provided key and value in their generic forms to the map.
METHOD Insert_Generic : I_Map
VAR_INPUT
	Key : T_Generic; // Key used to identify stored value
	Value : T_Generic; // Value to store in map with key
	bUpdate : BOOL; // If this is true the value will be updated if the key already exists.
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
END_VAR
VAR
	pCurrent, pPrevious : POINTER TO T_AVL_Map_Node;
	Tmp : T_Generic;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Insert_Generic := THIS^;

pCurrent := THIS^.pRoot;
pPrevious := pCurrent;

WHILE pCurrent <> 0 DO
	pPrevious := pCurrent;
	CASE F_Compare_Generics(Key, pCurrent^.Key) OF
		T_Comparison.Less 	 : pCurrent := pCurrent^.pLeft;
		T_Comparison.Greater : pCurrent := pCurrent^.pRight;
		T_Comparison.Equal 	 : 
			IF bUpdate THEN
				Tmp := F_Clone_Generic(Value, bSuccess => bSuccess, Error => THIS^.Error);
				IF NOT bSuccess THEN RETURN; END_IF
				F_Delete_Generic(pCurrent^.Value);
				pCurrent^.Value := Tmp;
			ELSE
				THIS^._Error_Status := GVL_Errors.ERR_DUPLICATE_KEY;
				END_IF
			RETURN;
	ELSE
		THIS^._Error_Status := GVL_Errors.ERR_INVALID_COMPARISON;
		RETURN;
		END_CASE
	END_WHILE
	
pCurrent := THIS^.Create_Node(Key, Value, bSuccess => bSuccess, Error => THIS^.Error);

IF NOT bSuccess THEN RETURN; END_IF

IF pPrevious = 0 THEN
	THIS^.pRoot := pCurrent;
ELSE
	CASE F_Compare_Generics(Key, pPrevious^.Key) OF
		T_Comparison.Less: pPrevious^.pLeft := pCurrent;
	ELSE
		pPrevious^.pRight := pCurrent;
		END_CASE
	END_IF
	
pCurrent^.pParent := pPrevious;
	
THIS^.Balance(pCurrent);
THIS^.Increment_Count();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Move_Node" Id="{76c66d62-dc2d-0f9c-397d-e7b209547250}" FolderPath="Internal\">
      <Declaration><![CDATA[// Moves from node at source into the position of the destination.
METHOD PROTECTED Move_Node
VAR_INPUT
	pDestination, pSource : POINTER TO T_AVL_Map_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pDestination = 0 OR_ELSE pSource = 0 THEN RETURN; END_IF 

IF pDestination^.pParent = 0 THEN
	THIS^.pRoot := pSource;
ELSIF pDestination = pDestination^.pParent^.pLeft THEN
	pDestination^.pParent^.pLeft := pSource;
ELSE
	pDestination^.pParent^.pRight := pSource;
	END_IF
	
IF pSource <> 0 THEN
	pSource^.pParent := pDestination^.pParent;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Remove" Id="{09343105-dbcd-0203-11b1-ef067a0963db}">
      <Declaration><![CDATA[// Removes value with the specified key from the map.
METHOD Remove : I_Map
VAR_INPUT
	Key : ANY; // Key used to identify stored value
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Remove := THIS^.Remove_Generic(Key, bSuccess => bSuccess);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Remove_Generic" Id="{9b157c43-482b-0cc4-1cc9-b2651ebee5dc}">
      <Declaration><![CDATA[// Removes value with the specified key in its generic form from the map.
METHOD Remove_Generic : I_Map
VAR_INPUT
	Key : T_Generic; // Key used to identify stored value
END_VAR
VAR_OUTPUT
	bSuccess : BOOL; // Indicates whether operation was successful. Success = TRUE, Failure = FALSE
END_VAR
VAR
	pTmp : POINTER TO T_AVL_Map_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Remove_Generic := THIS^;

pTmp := THIS^.Search_Node(THIS^.pRoot, Key);

IF pTmp = 0 THEN
	THIS^._Error_Status := GVL_Errors.ERR_NO_KEY;
	RETURN;
	END_IF

THIS^.Balance(THIS^.Remove_Node(pTmp));

THIS^.Decrement_Count();
THIS^.Clear_Error();

IF THIS^._Empty THEN THIS^.pRoot := 0; END_IF

bSuccess := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Remove_Node" Id="{af88455b-88f3-0a73-0f3b-559151760fed}" FolderPath="Internal\">
      <Declaration><![CDATA[// Finds and removes a node from a tree/sub-tree and returns the new root of the tree/subtree.
METHOD PROTECTED Remove_Node : POINTER TO T_AVL_Map_Node
VAR_INPUT
	pNode : REFERENCE TO POINTER TO T_AVL_Map_Node;
END_VAR
VAR
	pSuccessor : POINTER TO T_AVL_Map_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (NOT __ISVALIDREF(pNode)) OR_ELSE pNode = 0 THEN RETURN; END_IF 

IF pNode^.pLeft = 0 THEN
	THIS^.Move_Node(pNode, pNode^.pRight);
	Remove_Node := pNode^.pRight;
ELSIF pNode^.pRight = 0 THEN 
	THIS^.Move_Node(pNode, pNode^.pLeft);
	Remove_Node := pNode^.pLeft;
ELSE
	Remove_Node := pSuccessor := THIS^.Find_Minimum_Node(pNode^.pRight);
	IF pSuccessor^.pParent <> pNode THEN 
		THIS^.Move_Node(pSuccessor, pSuccessor^.pRight);
		pSuccessor^.pRight := pNode^.pRight;
		pSuccessor^.pRight^.pParent := pSuccessor;
		END_IF
	THIS^.Move_Node(pNode, pSuccessor);
	pSuccessor^.pLeft := pNode^.pLeft;
	pSuccessor^.pLeft^.pParent := pSuccessor;
	END_IF

THIS^.Delete_Node(pNode);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Rotate_Left" Id="{e95d8be2-2206-063f-0cbf-6e39c452db5f}" FolderPath="Internal\">
      <Declaration><![CDATA[// Rotates a tree/sub-tree left.
METHOD PROTECTED Rotate_Left
VAR_INPUT
    pX : POINTER TO T_AVL_Map_Node;
END_VAR
VAR
    pY : POINTER TO T_AVL_Map_Node := pX^.pRight;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pY <> 0 THEN
	pX^.pRight := pY^.pLeft;
	IF pY^.pLeft <> 0 THEN pY^.pLeft^.pParent := pX; END_IF
	pY^.pParent := pX^.pParent;
	END_IF
	
IF pX^.pParent = 0 THEN
	THIS^.pRoot := pY;
ELSIF pX = pX^.pParent^.pLeft THEN
	pX^.pParent^.pLeft := pY;
ELSE
	pX^.pParent^.pRight := pY;
	END_IF
	
IF pY <> 0 THEN pY^.pLeft := pX; END_IF
	
pX^.pParent := pY;
THIS^.Update_Height(pX);
THIS^.Update_Height(pY);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Rotate_Right" Id="{9d059779-9907-01bd-1762-6cce2f39d0c1}" FolderPath="Internal\">
      <Declaration><![CDATA[// Rotates a tree/sub-tree right.
METHOD PROTECTED Rotate_Right
VAR_INPUT
    pX : POINTER TO T_AVL_Map_Node;
END_VAR
VAR
    pY : POINTER TO T_AVL_Map_Node := pX^.pLeft;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF pY <> 0 THEN
	pX^.pLeft := pY^.pRight;
	IF pY^.pRight <> 0 THEN pY^.pRight^.pParent := pX; END_IF
	pY^.pParent := pX^.pParent;
	END_IF
	
IF pX^.pParent = 0 THEN
	THIS^.pRoot := pY;
ELSIF pX = pX^.pParent^.pLeft THEN
	pX^.pParent^.pLeft := pY;
ELSE
	pX^.pParent^.pRight := pY;
	END_IF
	
IF pY <> 0 THEN pY^.pRight := pX; END_IF
	
pX^.pParent := pY;
THIS^.Update_Height(pX);
THIS^.Update_Height(pY);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Search_Node" Id="{796364aa-22e1-083b-2885-71b19388c69a}" FolderPath="Internal\">
      <Declaration><![CDATA[// Checks if an item is in a tree/ sub-tree.
METHOD PROTECTED Search_Node : POINTER TO T_AVL_Map_Node
VAR_INPUT
	pNode : POINTER TO T_AVL_Map_Node;
	Key : T_Generic;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[WHILE pNode <> 0 DO
	CASE F_Compare_Generics(Key, pNode^.Key) OF
		T_Comparison.Less	: pNode := pNode^.pLeft;
		T_Comparison.Greater: pNode := pNode^.pRight; 
		T_Comparison.Equal	: 
			Search_Node := pNode;
			RETURN;
		END_CASE
	END_WHILE]]></ST>
      </Implementation>
    </Method>
    <Method Name="Update" Id="{d9712660-e5bd-0573-13fe-b31adac2059e}">
      <Declaration><![CDATA[// Updates the value at the provided key with the new value in the map.
METHOD Update : I_Map
VAR_INPUT
	Key 	: ANY; // Key used to identify stored value
	Value 	: ANY; // Value to store in map with key
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
END_VAR
VAR
	pTmp : POINTER TO T_AVL_Map_Node;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Update := THIS^.Update_Generic(Key, Value, bSuccess => bSuccess);]]></ST>
      </Implementation>
    </Method>
    <Method Name="Update_Generic" Id="{5f71dd77-c818-070f-1ac2-aa5f9d7f80e4}">
      <Declaration><![CDATA[// Updates the value at the provided key with the new value in their generic forms in the map.
METHOD Update_Generic : I_Map
VAR_INPUT
	Key 	: T_Generic; // Key used to identify stored value
	Value 	: T_Generic; // Value to store in map with key
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
END_VAR
VAR
	pTmp : POINTER TO T_AVL_Map_Node;
	Tmp : T_Generic;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Update_Generic := THIS^;

pTmp := THIS^.Search_Node(THIS^.pRoot, Key);

bSuccess := pTmp <> 0;

IF NOT bSuccess THEN 
	THIS^._Error_Status := GVL_Errors.ERR_NO_KEY;
	RETURN;
	END_IF
	
Tmp := F_Clone_Generic(Value, bSuccess => bSuccess, Error => THIS^.Error);

IF NOT bSuccess THEN RETURN; END_IF

F_Delete_Generic(pTmp^.Value);
pTmp^.Value := Tmp;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Update_Height" Id="{737a5ec5-78a4-0fe0-3641-d0b8bfba7973}" FolderPath="Internal\">
      <Declaration><![CDATA[METHOD PROTECTED Update_Height
VAR_INPUT
	pNode : POINTER TO T_AVL_Map_Node;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pNode^.Height := 1 + MAX(THIS^.Get_Height(pNode^.pLeft), THIS^.Get_Height(pNode^.pRight));]]></ST>
      </Implementation>
    </Method>
    <Method Name="Upsert" Id="{8aa218e3-a545-0da1-0329-59aadb46039b}">
      <Declaration><![CDATA[// Updates a value in the map for a given key. If the key doesnt exist, it will be inserted.
METHOD Upsert : I_Map
VAR_INPUT
	Key 	: ANY; // Key used to identify stored value
	Value 	: ANY; // Value to store in map with key
END_VAR
VAR_OUTPUT
	bSuccess : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[Upsert := THIS^.Insert_Generic(Key, Value, TRUE, bSuccess => bSuccess);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>